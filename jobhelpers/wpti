#!/usr/bin/perl

use strict;
use warnings;

use File::Temp qw/tempdir/;
use Getopt::Std;


my $tmpdir = tempdir(CLEANUP => 1);

$ENV{PGHOST} = $tmpdir;
$ENV{PGPORT} = $ENV{WPTI_PGPORT} || 54321;
$ENV{PGDATA} = $tmpdir."/pgdata";
$ENV{PGUSER} = 'postgres';

my $listen_addresses = '';
my $locale_opt = '';
my $extra_config = '';

my %opts;
getopts('CLc:', \%opts);
$listen_addresses = 'localhost' if $opts{L};
$locale_opt = '--locale=C' if $opts{C};
$extra_config = $opts{c};

my $ret;

system "initdb -A trust -U postgres $locale_opt >/dev/null";
if ($extra_config) {
    open CONF, ">> $ENV{PGDATA}/postgresql.conf" or die "could not open postgresql.conf: $!";
    print CONF $extra_config, "\n";
    close CONF;
}
$ret = system 'pg_ctl', '-s', '-w', '-l', "$tmpdir/logfile", '-o', "--fsync=off -k $tmpdir --listen-addresses='$listen_addresses'", 'start';

if ($ret != 0) {
    system('cat', "$tmpdir/logfile");
    exit 88;
}

if (@ARGV) {
    $ret = system @ARGV;
} else {
    $ret = system $ENV{SHELL};
}

system 'pg_ctl', '-s', '-w', '-m', 'immediate', 'stop';

if ($ret == -1) {
    exit 254;
} elsif ($ret & 127) {
    exit 255;
} else {
    exit($ret >> 8);
}
